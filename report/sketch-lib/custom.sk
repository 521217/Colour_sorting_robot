## An example of kinematics diagram for a 6-DOF robot arm
## 
## Author : Alex Dumitrache <alex@cimr.pub.ro>
## Customized by: Max <521217@student.inholland.nl>
## License: GPL

def blue<>
input{lib/defaults.sk}
input{lib/objects.sk}

def J1 0
def J2 -90
def J3 180
def J4 0
def J5 90
def J6 90

def arrowarc
{
    sweep [style=thick]{215, rotate(1, [Z])} (1,0)
    def p1 (1,0) then rotate(210, [Z])
    def p2 (1,0) then rotate(210+30, [Z])
    line [style=thick, style=->] (p1)(p2)
}

% The main drawing
put {scale(0.5)}
{   
    # base
    put {scale(1)}
    {
        put{scale([8,8,0.5])}{box}
        line [style=dashed] (0,0,0.2)(0,0,12)

        # link1
        put {translate([0,0,6]) * rotate(J1, [Z])}
        {
            put{scale([1,1,3])}{cylinder}
        
            # link2
            put {translate([0,0,6]) * rotate(-90, [X]) * rotate(J2+90, [Z])}
            {
                put{scale([1,1,3])}{cylinder}
                line [style=dashed] (0,0,0)(0,-7,0)

                # link3
                put {translate([0,-6,0]) * rotate(J3-180, [Z]) * rotate(90, [Y]) * rotate(90, [Z])}
                {
                    put{rotate(90, [X]) * scale([1,1,3])}{cylinder}
                    line [style=dashed] (0,0,0)(0,0,6)

                    # link4
                    put {translate([0,0,6]) * rotate(180+J4, [Z])}
                    {
                        # link5
                        put {rotate(-90, [X]) * rotate(180, [Y])}
                        {
                            put{scale([1,1,3])}{cylinder}

                            # link6
                            put {rotate(J5-90, [Z]) * translate([6,0,0]) * rotate(90, [Y]) * rotate(90, [Z])}
                            {
                                line [style=dashed] (0,0,.25)(0,0,-5)

                                #end-effector
                            	put{scale([3,1,1]) * translate([0,-1.5,0]) * rotate(90, [Y])}{cylinder}
                            	put{scale([3,1,1]) * translate([0,1.5,0]) * rotate(90, [Y])}{cylinder}
                            }
                        }
                    }
                }
            }
        }
    }
}



global 
{ 
    language tikz 
    camera rotate(0, (1,0,0)) * view((1.5,2,1),(0,0,0),[0,0,1]) * rotate(110, (0,0,1))
}


